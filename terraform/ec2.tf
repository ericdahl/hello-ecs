resource "aws_autoscaling_group" "app" {
  name                 = "tf-test-asg"
  vpc_zone_identifier  = aws_subnet.main.*.id
  min_size             = var.asg_min
  max_size             = var.asg_max
  desired_capacity     = var.asg_desired
  launch_configuration = aws_launch_configuration.app.name
}

resource "aws_launch_configuration" "app" {
  security_groups = [
    aws_security_group.instance_sg.id,
  ]

  key_name = var.key_name

  image_id      = "ami-62d35c02"
  instance_type = var.instance_type

  iam_instance_profile = aws_iam_instance_profile.app.name
  user_data            = <<EOF
#!/bin/bash

echo 'ECS_CLUSTER=terraform_example_ecs_cluster' > /etc/ecs/ecs.config
start ecs
EOF


  associate_public_ip_address = true

  lifecycle {
    create_before_destroy = true
  }
}

resource "aws_security_group" "instance_sg" {
  description = "controls direct access to application instances"
  vpc_id      = aws_vpc.main.id
  name        = "tf-ecs-instsg"

  ingress {
    protocol  = "tcp"
    from_port = 22
    to_port   = 22

    cidr_blocks = [
      var.admin_cidr_ingress,
    ]
  }

  ingress {
    from_port = 0
    to_port   = 0
    protocol  = "-1"

    security_groups = [
      aws_security_group.lb_sg.id,
    ]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

